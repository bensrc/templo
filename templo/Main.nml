/*
 *  Templo compiler
 *  Copyright (c)2008 Motion-Twin
 *
 *  This library is free software; you can redistribute it and/or
 *  modify it under the terms of the GNU Lesser General Public
 *  License as published by the Free Software Foundation; either
 *  version 2.1 of the License, or (at your option) any later version.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *  Lesser General Public License or the LICENSE file for more details.
 */
open Templo.Ast;

type target {
	TNeko;
	TPhp;
}

function report(msg,p) {
	if p == Lexer.null_pos then	{
		Stack.dump IO.stderr Stack.exc();
		IO.printf IO.stderr "Exception : %s\n" msg
	} else
	IO.printf IO.stderr "%s:%d: %s\n" (Lexer.source p,Lexer.line p,msg);
	Sys.exit(-1);
}

function parse(file) {
	var ch = IO.read_file file true;
	var lex = Lexer.create Buffer.create();
	Lexer.input lex file ch 1 0;
	var el = Templo.Parser.parse lex;
	IO.close_in ch;
	el
}

function complete_path(p) {
	var len = String.length p;
	if len > 0 && String.get p (len - 1) != '/' && String.get p (len - 1) != '\\' then
		p + "/"
	else
		p
}

try {
	var head = "Temploc v2.0 - (c)2008 Motion-Twin\nUsage : temploc [options] <files>";
	var files = &[];
	var output = &"";
	var target = &TNeko;
	var chk = Templo.Checker.init();
	var macros = &[];
	var decl = [
		("-output",Args.String (function(o) output := complete_path o),"<dir> : specify output directory");
		("-macros",Args.String (function(f) macros := f :: *macros),"<file> : add macro definition file");
		("-php",Args.Void (function() target := TPhp),": use php output");
	];
	Args.parse head decl (function(file) files := file :: *files);
	var macros = List.map (function(file) {
		var el = parse file;
		var el = Templo.Checker.add_macros chk file el;
		var el, deps = Templo.Checker.run chk el;
		(file,el,deps)
	}) List.rev(*macros);
	var files = List.map (function(file) {
		var el = parse file;
		var el, deps = Templo.Checker.run chk el;
		(file,el,deps)
	}) List.rev(*files);
	var datas = List.append macros files;
	match *target {
	| TNeko -> Templo.Neko.generate (*output) datas
	| TPhp -> assert()
	}
} catch {
	| Templo.Lexer.Error(msg,pos) -> report Templo.Lexer.error_msg(msg) pos
	| Templo.Parser.Error(msg,pos) -> report Templo.Parser.error_msg(msg) pos
	| Templo.Checker.Error(msg,pos) -> report msg pos
	| e ->
		Stack.dump IO.stderr Stack.exc();
		IO.printf IO.stderr "Exception : %s\n" string(e);
		Sys.exit (-1);
}
